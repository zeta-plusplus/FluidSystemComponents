within FluidSystemComponents.Sensor;

model Pt_FluidWithMach_DispColor00
    //----------------------------------------
  // Import
  //----------------------------------------
  import Colors = Modelica.Mechanics.MultiBody.Visualizers.Colors;
  import Modelica.Constants;
  import Modelica.Utilities.Streams;
  import PropulsionSystem.Types.switches;
  import units = Modelica.Units.SI;
  
  //----------------------------------------
  // replaceable
  //----------------------------------------
  replaceable package Medium = Modelica.Media.Interfaces.PartialMedium "Medium in the component" annotation(
    choicesAllMatching = true);
  
  
  //----------------------------------------
  // parameter
  //----------------------------------------
  parameter Visualizers.Types.SwitchUnitVisPressure switchUnitP = Visualizers.Types.SwitchUnitVisPressure.kPa;
  parameter Integer sigDigits(min = 1) = 6 "";
  parameter Real valMin = 100 "";
  parameter Real valMax = 1000 "";
  parameter Real colorMap[:, 3] = Colors.ColorMaps.jet();
  
  
  //----------------------------------------
  // variables
  //----------------------------------------
  Real vecRGB[3];
  Real pVis;
  Real pMinContour;
  Real pMaxContour;
  
  units.SpecificEntropy s_fluidStat "specific entropy, fluidStat";
  units.SpecificEntropy s_fluid "specific entropy, fluid";
  units.Velocity Vsound(min = 340) "static temperature refered";
  units.Velocity V(min = 340*2);
  Real Mn;
  
  
  //----------------------------------------
  // Internal objects
  //----------------------------------------
  Medium.BaseProperties fluid(T(min = 0, start = 288.15, nominal = 288.15), p(min = 0, start = 1e5)) "fluid obj, total";
  Medium.BaseProperties fluidStat(T(min = 0, start = 288.15, nominal = 288.15), p(min = 0, start = 1e5)) "fluid station obj, static";
  
  //----------------------------------------
  // interface
  //----------------------------------------
  FluidSystemComponents.Interfaces.FluidWithMach_a port(redeclare package Medium = Medium) annotation(
    Placement(visible = true, transformation(origin = {-100, 0}, extent = {{-10, -10}, {10, 10}}, rotation = 0), iconTransformation(origin = {-100, -2}, extent = {{-10, -10}, {10, 10}}, rotation = 0)));
equation
//
  port.portStat.m_flow = 0;
  fluidStat.p= port.portStat.p;
  fluidStat.h = actualStream(port.portStat.h_outflow);
  fluidStat.Xi = actualStream(port.portStat.Xi_outflow);
  port.portStat.C_outflow = zeros(Medium.nC);
  //
  port.portStat.h_outflow = fluidStat.h;
  port.portStat.Xi_outflow = fluidStat.Xi;
  //
  Mn= port.Mn;
  
  //----------
  //
  s_fluid = Medium.specificEntropy(fluid.state);
  s_fluidStat = Medium.specificEntropy(fluidStat.state);
//
  Vsound = Medium.velocityOfSound(fluidStat.state);
  Mn = V/Vsound;
//
  fluid.h = fluidStat.h + 1/2*V^2;
// isentropic between static and total
  s_fluidStat = s_fluid;
// mass conservation
  fluidStat.Xi = fluid.Xi;
  
//----------
 if (switchUnitP == Visualizers.Types.SwitchUnitVisPressure.kPa) then
    pVis = fluid.p/1000.0;
    pMinContour = valMin/1000.0;
    pMaxContour = valMax/1000.0;
  else
    pVis = fluid.p;
    pMinContour = valMin;
    pMaxContour = valMax;
  end if;
//----------
  vecRGB = Colors.scalarToColor(pVis, pMinContour, pMaxContour, colorMap);
  
  
  
  annotation(
    Icon(coordinateSystem(preserveAspectRatio = false, extent = {{-100, -100}, {100, 100}}), graphics = {Text(origin = {0, -116}, extent = {{-100, 10}, {100, -10}}, textString = DynamicSelect("0.0", String(pVis, sigDigits, 0, true))), Ellipse(fillColor = DynamicSelect({192, 192, 192}, {vecRGB[1], vecRGB[2], vecRGB[3]}), pattern = LinePattern.None, fillPattern = FillPattern.Solid,extent = {{-100, 100}, {100, -100}})}),
    defaultComponentName = "Pressure");





end Pt_FluidWithMach_DispColor00;